class Dangling;

terminal LPAREN, RPAREN, IF, ELSE, WHILE, LBRACE, RBRACE, ASSIGN, SEMI;
terminal NUMBER, ID;

nonterminal stmnt, matchedStmnt, unmatchedStmnt, stmntList, expr;

start with stmnt;

stmnt ::= matchedStmnt
       |  unmatchedStmnt
       ;

matchedStmnt ::= IF LPAREN expr RPAREN matchedStmnt ELSE matchedStmnt
              |  WHILE LPAREN expr RPAREN matchedStmnt
              |  LBRACE stmntList RBRACE
              |  ID ASSIGN expr SEMI
              ;

unmatchedStmnt ::= IF LPAREN expr RPAREN stmnt
                |  IF LPAREN expr RPAREN matchedStmnt ELSE unmatchedStmnt
                |  WHILE LPAREN expr RPAREN unmatchedStmnt
                ;
      
stmntList ::= stmntList stmnt 
           |  /* epsilon */
           ;

expr ::= NUMBER
      ;


  

